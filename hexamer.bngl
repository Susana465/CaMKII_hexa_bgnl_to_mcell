begin model

begin parameters
	# Initial number of Ca molecules/cell
  	Ca_i 10000 
	CaM_i 2000  
	CaMKII_i 2000  
	PP1_i 1000
	NMDAR_i 400

    NA_um3 6.022e8 # Avogadro's number mutiplied by 1e-15 since we are using volume units of microns(^-3)

    # R1 to R4:  
 	kon_CaCaM 1e+6/NA_um3   # Bimolecular rate constant for CaM-Ca binding (1/# 1/s)
	koff_1Ca 1.928   # Unimolecular rate constant for CaM_Ca unbinding (1/s)
	koff_2Ca 1.937
	koff_3Ca 7.476
	koff_4Ca 25.783

	# 5.1 CaMKII + CaM_Ca4 < - > CaMKII_CaM
	kon_cam_ca4_camkii 4.2e+6/NA_um3  
	koff_cam_ca4_camkii 2.5e-4

    # CaMKII flicker active/inactive
    kon_camkii_act 2e+4
	koff_camkii_inact 1e+7

	# CaMKII phosphorylation
	k_pCaM4 = 1e5 #0.96 #1/s #ref ordyan (1e5 in monomer model for susana's diss)
	k_pCaM4_P = 1e6
	# Dephosphorylation ordyan
	# k_cat = (31/1.25)/60 # where do these numbers come from?
	# K_m = 11*(NA*V) #uM 

	k_dephos_camkii_p 3e+3/NA_um3  # reaction number (8) on diss

	kon_9 2e+6/NA_um3
	koff_9 0.03

	# Units: volumes in um3
	VolCP 0.50588 
	VolPM 0.0307 # from volume of 0.505 r = 0.494 and area = 3.07um2 * 0.01um thickness. Assuming a thickness of 0.01 microns for the PM
end parameters

begin compartments
 	PM 2 VolPM #2Dimensions, surface compartment
 	CP 3 VolCP PM #3Dimensions and 1 microcubic in volume, volume compartment
 	# http://www.lehman.edu/academics/cmacs/documents/RuleBasedPrimer-2011.pdf
end compartments

begin molecule types
  Ca()
  PP1()
  CaM(ca~0~1~2~3~4,camkii)
  CaMKII(active~0~1,T286~0~P,S306~0~P,cam,nmdar,l,r,c)
  NMDAR(camk)
end molecule types

begin species
@CP:Ca() Ca_i
@CP:CaM(ca~0,camkii) CaM_i
# CaMKII hexamer: CaMKII_i
@CP:CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!6,r!1,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!1,r!2,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!2,r!3,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!3,r!4,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!4,r!5,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!5,r!6,c) CaMKII_i
# CaMKII dodecamer as per Ordyan's model:
# CaMKII(active~0,r!1,l!6,c!13,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!2,l!1,c!14,T286~0,S306~0,nmdar,cam).\
# CaMKII(active~0,r!3,l!2,c!15,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!4,l!3,c!16,T286~0,S306~0,nmdar,cam).\
# CaMKII(active~0,r!5,l!4,c!17,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!6,l!5,c!18,T286~0,S306~0,nmdar,cam).\
# CaMKII(active~0,r!7,l!12,c!13,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!8,l!7,c!14,T286~0,S306~0,nmdar,cam).\
# CaMKII(active~0,r!9,l!8,c!15,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!10,l!9,c!16,T286~0,S306~0,nmdar,cam).\
# CaMKII(active~0,r!11,l!10,c!17,T286~0,S306~0,nmdar,cam).CaMKII(active~0,r!12,l!11,c!18,T286~0,S306~0,nmdar,cam) CaMKII_i
@CP:PP1() PP1_i
@PM:NMDAR(camk) NMDAR_i
end species

begin reaction rules
# Ca binds to CaM progresively
#1  Ca + CaM < - > CaM_Ca1
	CaM(ca~0,camkii) + Ca() <-> CaM(ca~1,camkii) kon_CaCaM, koff_1Ca
#2  Ca + CaM_Ca1 < - > CaM_Ca2
 	CaM(ca~1,camkii) + Ca() <-> CaM(ca~2,camkii) kon_CaCaM, koff_2Ca
#3  Ca + CaM_Ca2 < - > CaM_Ca3
	CaM(ca~2,camkii) + Ca() <-> CaM(ca~3,camkii) kon_CaCaM, koff_3Ca
#4  Ca + CaM_Ca3 < - > CaM_Ca4
	CaM(ca~3,camkii) + Ca() <-> CaM(ca~4,camkii) kon_CaCaM, koff_4Ca

# Saturated CaM_Ca4 binds to CaMKII ring
#5.1 CaMKII + CaM_Ca4 < - > CaMKII_CaM
### DOCKED NEEDS TO BE ADDED OR NOT?
	CaM(ca~4,camkii) + CaMKII(cam,T286~0,nmdar,active~1) <-> CaM(ca~4,camkii!1).CaMKII(cam!1,T286~0,nmdar,active~1) kon_cam_ca4_camkii, koff_cam_ca4_camkii

# CaMKII subunits can flicker between active or inactive. (CaM binds to active CaMKII only)
 	CaMKII(cam,T286~0,nmdar,active~0) <-> CaMKII(cam,T286~0,nmdar,active~1) kon_camkii_act, koff_camkii_inact

# CaMKII phosphorylation
# When CaMKII_CaM_Ca4, neighbouring subunit if bound to CaM can P
	CaMKII(l!1,T286~0,cam!2).CaM(ca~4,camkii!2).CaMKII(r!1,cam!+) -> CaMKII(l!1,T286~P,cam!2).CaM(ca~4,camkii!2).CaMKII(r!1,cam!+) k_pCaM4
# When CaMKII_CaM_P, can phosphorylate neighbouring subunit
  	CaMKII(l!1,T286~0,cam!2).CaM(ca~4,camkii!2).CaMKII(r!1,T286~P) -> CaMKII(l!1,T286~P,cam!2).CaM(ca~4,camkii!2).CaMKII(r!1,T286~P) k_pCaM4_P	

# CaMKII dephosphorylation
	CaMKII(T286~P) + PP1 -> CaMKII(T286~0) + PP1 k_dephos_camkii_p

# CaMKII_P_Ca4 can bind to free NMDAR in the PM
	#CaMKII(l!1,T286~P,cam!2,nmdar).CaM(ca~4,camkii!2).CaMKII(r!1,cam!+) + NMDAR(camk) <-> CaMKII(l!1,T286~P,cam!2,nmdar!a).CaM(ca~4,camkii!2).CaMKII(r!1,cam!+).NMDAR(camk!a) kon_9,koff_9
	
# CaMKII + NMDAR
	CaMKII(nmdar,T286~P,active~1) + NMDAR(camk) <-> CaMKII(nmdar!1,T286~P,active~1).NMDAR(camk!1) kon_9,koff_9
	
end reaction rules

begin observables
  	Molecules CaM_free CaM(ca~0,camkii)
	Molecules CaM_Ca1 CaM(ca~1,camkii)
	Molecules CaM_Ca2 CaM(ca~2,camkii)
	Molecules CaM_Ca3 CaM(ca~3,camkii)
	Molecules CaM_Ca4 CaM(ca~4,camkii)

	Molecules PP1 PP1()

    Molecules CaMKII_inactive CaMKII(cam,T286~0,active~0)
	#Molecules CaMKII_ring_inactive CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!6,r!1,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!1,r!2,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!2,r!3,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!3,r!4,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!4,r!5,c).CaMKII(cam,nmdar,T286~0,S306~0,active~0,l!5,r!6,c)
	Molecules CaMKII_active CaMKII(T286~0,active~1)
	#Molecules CaMKII_ring_active CaMKII(active~1,l!6,r!1,c).CaMKII(active~1,l!1,r!2,c).CaMKII(active~1,l!2,r!3,c).CaMKII(active~1,l!3,r!4,c).CaMKII(active~1,l!4,r!5,c).CaMKII(active~1,l!5,r!6,c)
	#Molecules CaMKII_active_pairs CaMKII(T286~0,active~1).CaMKII(T286~0,active~1)
	Molecules CaMKII_P CaMKII(T286~P)
	#Molecules NMDAR_CaMKII_P_Ca4 CaMKII(l!1,T286~P,cam!2,nmdar!a).CaM(ca~4,camkii!2).CaMKII(r!1,cam!+).NMDAR(camk!a)
	Molecules CaMKII_P_NMDAR CaMKII(nmdar!1,T286~P,active~1).NMDAR(camk!1)
end observables


end model

simulate({method=>"nf",t_end=>5,n_steps=>100})